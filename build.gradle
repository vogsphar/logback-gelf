apply plugin: 'java'
apply plugin: 'findbugs'
apply plugin: 'checkstyle'
apply plugin: 'maven'
apply plugin: 'signing'

group = "de.siegmar"
archivesBaseName = "logback-gelf"
version = "1.1.0"

sourceCompatibility = 1.7

repositories {
    jcenter()
}

dependencies {
    compile 'ch.qos.logback:logback-classic:1.1.8'
    testCompile 'junit:junit:4.12'
    testCompile 'com.google.guava:guava:19.0'
    testCompile 'com.fasterxml.jackson.core:jackson-databind:2.7.2'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

checkstyle {
    configFile = file("${rootDir}/config/checkstyle/checkstyle.xml")
    configProperties = [samedir: "${rootDir}/config/checkstyle", project_loc: "${rootDir}"]
}

tasks.withType(FindBugs) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

if (hasProperty('signing.keyId')) {
    signing {
        sign configurations.archives
    }
}

if (hasProperty('ossrhUsername')) {
    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                pom.project {
                    name 'Logback GELF'
                    packaging 'jar'
                    description 'Logback appender for sending GELF messages with zero additional dependencies.'
                    url 'https://github.com/osiegmar/logback-gelf'

                    scm {
                        connection 'scm:git:https://github.com/osiegmar/logback-gelf.git'
                        developerConnection 'scm:git:https://github.com/osiegmar/logback-gelf.git'
                        url 'https://github.com/osiegmar/logback-gelf'
                    }

                    licenses {
                        license {
                            name 'GNU Lesser General Public License version 2.1'
                            url 'http://www.gnu.org/licenses/old-licenses/lgpl-2.1.txt'
                        }
                    }

                    developers {
                        developer {
                            id 'osiegmar'
                            name 'Oliver Siegmar'
                            email 'oliver@siegmar.de'
                        }
                    }
                }
            }
        }
    }
}
